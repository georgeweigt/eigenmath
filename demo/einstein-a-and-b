-- spontaneous emission

A21 = 256 alpha^4 c / (6561 a0)

-- induced emission

B21 = c^2 / (2 h nu^3) A21

-- absorption

p1 = 2
p2 = 6

B12 = p2 / p1 B21

"Einstein A and B coefficients for Lyman-alpha line"

A21
B21
B12

"Verify Einstein's equilibrium formula"

rho = (2 h nu^3 / c^2) / (exp(h nu / (k T)) - 1)

E(n) = -alpha^2 mu c^2 / (2 n^2)

nu = (E(2) - E(1)) / h

P = B12 rho p1 exp(-E(1) / (k T))
Q = B21 rho p2 exp(-E(2) / (k T)) + A21 p2 exp(-E(2) / (k T))

check(P == Q)

"ok"

-- CODATA Internationally recommended 2022 values
-- https://physics.nist.gov/cuu/Constants/
-- physical constants (c, e, h, and k are exact values)

a0 = 5.29177210544 10^(-11) meter
alpha = 7.2973525643 10^(-3)
c = 299792458.0 meter / second
e = 1.602176634 10^(-19) coulomb
epsilon0 = 8.8541878188 10^(-12) farad / meter
h = 6.62607015 10^(-34) joule second
hbar = h / float(2 pi)
k = 1.380649 10^(-23) joule / kelvin
me = 9.1093837139 10^(-31) kilogram
mp = 1.67262192595 10^(-27) kilogram
mu0 = 1.25663706127 10^(-6) newton / ampere^2

coulomb = ampere second
farad = coulomb / volt
joule = kilogram meter^2 / second^2
newton = kilogram meter / second^2
tesla = kilogram / second^2 / ampere
volt = joule / coulomb

ampere = "ampere"
kelvin = "kelvin"
kilogram = "kilogram"
meter = "meter"
second = "second"

pi = float(pi) -- use numerical value of pi
mu = me mp / (me + mp)
a0 = a0 me / mu -- correction

a0
nu
A21
B21
B12
