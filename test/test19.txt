-- Draw probability density for high energy Moller scattering

-- eigenmath.org/moller-scattering-3.txt

pi = float(pi) -- use numerical value of pi
N = 6          -- number of bins
a = pi / N

I(theta) = -cos(theta) - 8 cos(theta) / sin(theta)^2

C = I(pi - a) - I(a) -- normalization constant

"Probability density"

-- f is the probability density function for Moller scattering

f = 1/C (cos(theta)^2 + 3)^2 / sin(theta)^3
f

xrange = (a,pi - a)
yrange = (0,2)
draw(f,theta)

"Probability distribution"

P = zero(N)

for(k,2,N - 1,
  theta2 = k pi/N,
  theta1 = theta2 - pi/N,
  P[k] = I(theta2) - I(theta1)
)

P = P / sum(k,1,N,P[k]) -- normalize
P

"Verify probability density formula (1=ok)"

F = -expcos(theta) - 8 expcos(theta) / expsin(theta)^2

f = (expcos(theta)^2 + 3)^2 / expsin(theta)^3

d(F,theta) == f

exit