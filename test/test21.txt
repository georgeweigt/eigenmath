-- R returns a radial eigenfunction

R(n,l) = 2 / n^2 *
         a0^(-3/2) *
         sqrt((n - l - 1)! / (n + l)!) *
         (2 r / (n a0))^l *
         L(2 r / (n a0),n - l - 1,2 l + 1) *
         exp(-r / (n a0))

-- L returns a Laguerre function

L(x,n,m) = (n + m)! sum(k,0,n,(-x)^k / ((n - k)! (m + k)! k!))

-- print radial eigenfunctions

R(1,0)
R(2,0)
R(2,1)
R(3,0)
R(3,1)
R(3,2)

"Verify radial eigenfunctions (1=ok)"

rho = r / a0
A = a0^(-3/2)

R(1,0) == A 2 exp(-rho)
R(2,0) == A sqrt(2)/4 (2 - rho) exp(-rho/2)
R(2,1) == A sqrt(6)/12 rho exp(-rho/2)
R(3,0) == A 2 sqrt(3)/27 (3 - 2 rho + 2/9 rho^2) exp(-rho/3)
R(3,1) == A sqrt(6)/81 rho (4 - 2/3 rho) exp(-rho/3)
R(3,2) == A 2 sqrt(30)/1215 rho^2 exp(-rho/3)
