"Compton scattering 3"

clear

-- Verify lab frame formulas for Compton scattering

gmunu = ((1,0,0,0),(0,-1,0,0),(0,0,-1,0),(0,0,0,-1))

-- momentum vectors in center of mass frame

E = sqrt(omega^2 + m^2)

p1 = (omega, 0, 0, omega)
p2 = (E, 0, 0, -omega)

p3 = (omega,
      omega expsin(theta) expcos(phi),
      omega expsin(theta) expsin(phi),
      omega expcos(theta))

p4 = (E,
      -omega expsin(theta) expcos(phi),
      -omega expsin(theta) expsin(phi),
      -omega expcos(theta))

s = dot(p1 + p2,gmunu,p1 + p2)
t = dot(p1 - p3,gmunu,p1 - p3)
u = dot(p1 - p4,gmunu,p1 - p4)

f11 = 32 dot(p1,gmunu,p2) dot(p1,gmunu,p4) +
      64 m^2 dot(p1,gmunu,p2) -
      32 m^2 dot(p1,gmunu,p3) -
      32 m^2 dot(p1,gmunu,p4) + 32 m^4

f12 = 16 m^2 dot(p1,gmunu,p2) -
      16 m^2 dot(p1,gmunu,p4) + 32 m^4

f22 = 32 dot(p1,gmunu,p2) dot(p1,gmunu,p4) +
      32 m^2 dot(p1,gmunu,p2) -
      32 m^2 dot(p1,gmunu,p3) -
      64 m^2 dot(p1,gmunu,p4) + 32 m^4

-- Verify probability density in center of mass frame

check(f11 == -8 s u + 24 s m^2 + 8 u m^2 + 8 m^4)
check(f12 == 8 s m^2 + 8 u m^2 + 16 m^4)
check(f22 == -8 s u + 8 s m^2 + 24 u m^2 + 8 m^4)

-- transform momentum vectors to lab frame

Lambda = ((E/m,0,0,omega/m),(0,1,0,0),(0,0,1,0),(omega/m,0,0,E/m))

p1 = dot(Lambda,p1)
p2 = dot(Lambda,p2)
p3 = dot(Lambda,p3)
p4 = dot(Lambda,p4)

-- Verify invariance of Mandelstam variables

check(s == dot(p1 + p2,gmunu,p1 + p2))
check(t == dot(p1 - p3,gmunu,p1 - p3))
check(u == dot(p1 - p4,gmunu,p1 - p4))

-- Verify lab variables"

omegaL = p1[1]
omegaLp = p3[1]

check(s == 2 m omegaL + m^2)
check(t == 2 m (omegaLp - omegaL))
check(u == -2 m omegaLp + m^2)

-- Verify probability density"

omegaL = quote(omegaL)    -- clear omegaL
omegaLp = quote(omegaLp)  -- clear omegaLp

s = 2 m omegaL + m^2
t = 2 m (omegaLp - omegaL)
u = -2 m omegaLp + m^2

f11 = -8 s u + 24 s m^2 + 8 u m^2 + 8 m^4
f12 = 8 s m^2 + 8 u m^2 + 16 m^4
f22 = -8 s u + 8 s m^2 + 24 u m^2 + 8 m^4

d11 = (s - m^2)^2
d12 = (s - m^2) (u - m^2)
d22 = (u - m^2)^2

A = 1/4 (f11/d11 + 2 f12/d12 + f22/d22)

B = 2 (omegaLp/omegaL + omegaL/omegaLp + (m/omegaL - m/omegaLp + 1)^2 - 1)

check(A == B)
