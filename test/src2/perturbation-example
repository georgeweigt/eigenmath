"Perturbation example"

clear

ca0 = a
cb0 = b

da = -i/hbar Hab(t) exp(-i omega t) cb
db = -i/hbar Hba(t) exp(i omega t) ca

da1 = eval(da, cb, cb0)
db1 = eval(db, ca, ca0)

--"Verify equation (1)"
check(da1 == -i b / hbar Hab(t) exp(-i omega t))
check(db1 == -i a / hbar Hba(t) exp(i omega t))
--"ok"

ca1 = ca0 + integral(da1,t)
cb1 = cb0 + integral(db1,t)

--"Verify equation (2)"
check(ca1 == a - i b / hbar integral(Hab(t) exp(-i omega t), t))
check(cb1 == b - i a / hbar integral(Hba(t) exp(i omega t), t))
--"ok"

da2 = eval(da, cb, cb1)
db2 = eval(db, ca, ca1)

--"Verify equation (3)"
Ta = -i b / hbar Hab(t) exp(-i omega t) -
     a / hbar^2 Hab(t) exp(-i omega t) *
     integral(Hba(t) exp(i omega t), t)
Tb = -i a / hbar Hba(t) exp(i omega t) -
     b / hbar^2 Hba(t) exp(i omega t) *
     integral(Hab(t) exp(-i omega t), t)
check(da2 == Ta)
check(db2 == Tb)
--"ok"

ca2 = ca0 + integral(da2,t)
cb2 = cb0 + integral(db2,t)

--"Verify equation (4)"
I = integral(Hba(t) exp(i omega t), t)
T = a - i b / hbar integral(Hab(t) exp(-i omega t), t) -
    a / hbar^2 integral(Hab(t) exp(-i omega t) I, t)
check(ca2 == T)
--"ok"

--"Verify equation (5)"
I = integral(Hab(t) exp(-i omega t), t)
T = b - i a / hbar integral(Hba(t) exp(i omega t), t) -
    b / hbar^2 integral(Hba(t) exp(i omega t) I, t)
check(cb2 == T)
--"ok"
