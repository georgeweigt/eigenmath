-- Draw angular distributions for Compton scattering

-- eigenmath.org/compton-scattering-4.txt

omegap = m omega / (m + omega (1 - cos(theta)))

M = omega/omegap + omegap/omega + cos(theta)^2 - 1

f = (omegap/omega)^2 M sin(theta)

m = 0.511 -- MeV

pie = float(pi)

-- I integrates f numerically from a to b

I(a,b) = (b - a)/100 sum(k,0,99,eval(f,theta,a + k (b - a)/100))

xrange = (0,pie)
yrange = (0,1)

"Scattering angle probability density 0.05 MeV"

omega = 0.05
C = I(0,pie)
draw(f/C,theta)

"Scattering angle probability density 0.5 MeV"

omega = 0.5
C = I(0,pie)
draw(f/C,theta)

"Scattering angle probability density 5 MeV"

omega = 5.0
C = I(0,pie)
draw(f/C,theta)

"Scattering angle probability distribution 0.511 MeV"

omega = m

N = 4
P = zero(N)
C = I(0,pie)

for(k,0,N - 1,P[k + 1] = I(k pie/N,(k + 1) pie/N)/C)

P

exit
